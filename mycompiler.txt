% prog -> {classDecl} {funcDef} 'main' funcBody ';'
prog ->  classDecls funcDefs 'main' funcBody ';'
classDecls -> classDecl classDecls | EPSILON
funcDefs -> funcDef funcDefs | EPSILON

% classDecl -> 'class' 'id' [':' 'id' {',' 'id'}] '{' {varDecl} {funcDecl} '}' ';'
classDecl -> 'class' 'id' classParent '{' varDecls funcDecls '}' ';' 
classParent -> ':' 'id' moreParent | EPSILON
moreParent -> ',' 'id' moreParent | EPSILON
varDecls -> varDecl varDecls | EPSILON
funcDecls -> funcDecl funcDecls |EPSILON

% funcDecl -> type 'id' '(' fParams ')' ';' 
funcDecl -> type 'id' '(' fParams ')' ';'

% funcHead ->  type ['id' 'sr'] 'id' '(' fParams ')'
funcHead -> type owner 'id' '(' fParams ')'
owner -> 'id' 'sr' | EPSILON

% funcDef -> funcHead funcBody ';'
funcDef -> funcHead funcBody ';'

% funcBody -> '{' {varDecl} {statement} '}'
funcBody -> '{' varDecls statements '}'
statements -> statement statements | EPSILON

% varDecl -> type 'id' {arraySize} ';'
varDecl -> type 'id' arraySizes ';'
arraySizes -> arraySize arraySizes | EPSILON

% statement -> assignStat ';'
%             |'if' '(' expr ')' 'then' statBlock 'else' statBlock ';'
%             |'for' '(' type 'id' assignOp expr ';' relExpr ';' assignStat ')' statBlock ';'
%             |'read' '(' variable ')' ';'
%             | 'write' '(' expr ')' ';'
%             |'return' '(' expr ')' ';'
statement -> assignStat ';'
             |'if' '(' expr ')' 'then' statBlock 'else' statBlock ';'
             |'for' '(' type 'id' assignOp expr ';' relExpr ';' assignStat ')' statBlock ';'
             |'read' '(' variable ')' ';'
             |'write' '(' expr ')' ';'
             |'return' '(' expr ')' ';'
             
% assignStat -> variable assignOp expr
assignStat -> variable assignOp expr

% statBlock -> '{' {statement} '}' | statement | EPSILON
statBlock -> '{' statements '}' | statement | EPSILON
statements -> statement statements| EPSILON

% expr -> arithExpr | relExpr
% expr -> arithExpr | relExpr -- rewrite due to (a)
% expr -> term arithExpr_ | term arithExpr_  relOp term arithExpr_ -- after rewriting, refactoring 
expr -> term arithExpr_  relExprTail
relExprTail -> relOp term arithExpr_ | EPSILON 


% relExpr -> arithExpr relOp arithExpr
% -- relExpr -> arithExpr relOp arithExpr  -- rewrite due to (a)
relExpr -> term arithExpr_  relOp term arithExpr_

% arithExpr -> arithExpr addOp term | term
% -- arithExpr -> arithExpr addOp term | term -- left recursion (a)
arithExpr -> term arithExpr_
arithExpr_ -> addOp term arithExpr_ | EPSILON



% sign -> '+' | '-'
sign -> '+' | '-'

% term -> term multOp factor | factor
% - term -> term multOp factor | factor -- left recursion (b )
term -> factor term_
term_ -> multOp factor term_ | EPSILON


% factor -> variable
%            | functionCall
%            | 'intNum' | 'floatNum'
%            | '(' arithExpr ')'
%            | 'not' factor
%            | sign factor

% factor -> variable | functionCall | 'intNum' | 'floatNum' | '(' arithExpr ')' | 'not' factor | sign factor -- variable && functionCall ='id' , refactoring (c)
factor -> variableOrfunctionCall | 'intNum' | 'floatNum' | '(' arithExpr ')' | 'not' factor | sign factor 
variableOrfunctionCall ->  idnests 'id'  variableOrfunctionCallTail
variableOrfunctionCallTail -> indices | '(' aParams ')'


% variable -> {idnest} 'id' {indice}
variable -> idnests 'id' indices
idnests -> idnest idnests | EPSILON
indices -> indice indices | EPSILON

% functionCall -> {idnest} 'id' '(' aParams ')'
% -- functionCall -> idnests 'id' '(' aParams ')' -- due to (c), no needed anymore

% idnest -> 'id' {indice} '.'
%           |'id' '(' aParams ')' '.'
% -- idnest -> 'id' indices '.' |'id' '(' aParams ')' '.' -- refactoring
idnest -> 'id' idnest_
idnest_ -> indices  | '(' aParams ')' '.' 

% indice -> '[' arithExpr ']'
indice -> '[' arithExpr ']'

% arraySize -> '[' 'intNum' ']'
arraySize -> '[' 'intNum' ']'

% type -> 'integer' | 'float' | 'id'
type -> 'integer' | 'float' | 'id'

% fParams -> type 'id' {arraySize} {fParamsTail} | EPSILON
fParams -> type 'id' arraySizes fParamsTails | EPSILON
fParamsTails -> fParamsTail fParamsTails | EPSILON

% aParams -> expr {aParamsTail} | EPSILON
%% aParams -> expr aParamsTails | EPSILON -- simply(d)
%% aParamsTails -> aParamsTail aParamsTails| EPSILON -- simply(d)
aParams -> aParams ',' expr | EPSILON

% fParamsTail -> ',' type 'id' {arraySize}
fParamsTail -> ',' type 'id' arraySizes

% aParamsTail -> ',' expr
%% aParamsTail -> ',' expr -- simply(d)


% assignOp -> '='
assignOp -> '='

% relOp -> 'eq' | 'neq' | 'lt' | 'gt' | 'leq' | 'geq'
relOp -> 'eq' | 'neq' | 'lt' | 'gt' | 'leq' | 'geq'

% addOp -> '+' | '-' | 'or'
addOp -> '+' | '-' | 'or'

% multOp -> '*' | '/' | 'and'
multOp -> '*' | '/' | 'and'

             

